== Usage
You can use the grails-audit-logging plugin in several ways.

=== Basic
To enable auditing using the default configuration, simply implement the `Auditable` trait. 

For example:

```groovy
class Author implements Auditable {
    String name
    Integer age
    
    static hasMany = [books: Book]
}
```

This will enable auditing for the domain using your configuration in `grails.plugin.auditLog` merged with the defaults from `DefaultAuditLogConfig.groovy` (in the plugin jar).

=== Context Overrides
There are many scenarios where you want to use a default configuration for most operations, but need to explicitly override for certain scenarios.

One fairly common example is the need to disable audit logging completely for a particular set of changes. This is often the case for bulk operations or operations that might be performed "by the system" such as cleanup, purge, expiration jobs, etc.

==== Audit Log Context
You can use the `AuditLogContext` object to override almost any configuration parameter for a given block

For example:

```groovy
// Enable verbose logging with full class name excluding only properties named 'ssn'
AuditLogContext.withConfig(verbose: true, logFullClassName: true, excluded: ['ssn']) { ... }

// Disable verbose logging
AuditLogContext.withConfig(verbose: false) { ... }

// Disable all logging 
AuditLogContext.withConfig(disabled: true) { ... }
```

The last two are so common, there's a method that does them by name:

```groovy
// Disable verbose logging
AuditLogContext.withoutVerboseAuditLog { ... }

// Disable all logging
AuditLogContext.withoutAuditLog { ... }
```

[WARNING]
====
The AuditLogContext stores the context using ThreadLocal, so configuration is only present for the current thread. If you start something asynchronously, be sure you setup the context within the new execution thread.
====

=== Domain Overrides
The context gives you programmatic control over configuration of auditing but you can go even finer grained, if needed, to control auditing on a per-entity basis.

To override auditing for a specific entity or based on runtime values, just override the trait method:

```groovy
// Assuming this domain has a status attribute, disable auditing for anything not active, for example
@Override
boolean isAuditable() {
    this.status == ACTIVE
}
```

Another example is to override which properties get ignored for a specific entity:

```groovy
@Override
Collection<String> getLogExcluded() {
    ['myField', 'version']
}
```

Control the specific whitelist of attributes logged:

```groovy
@Override
Collection<String> getLogIncluded() {
    ['whiteListField1', 'whiteListField2']
}
```

Customize the id that is logged for the entity, for example instead of using Author's `id` property, log the Author's name and age as `Tolkien:85`:

```groovy
@Override
String getLogEntityId() {
    "${name}:${age}"
}
```

The ability to override and augment the default behavior provided by the trait is what makes the trait implementation so flexible.

NOTE: Most of the `Auditable` methods rely on the AuditLogContext for defaults. If you override the trait method, ensure you consider the keeping the default behavior if you are supplementing.

=== Customized Auditable Trait
You could also extend the trait entirely to override the default auditing behavior:

```groovy
trait MyAuditable<D> extends Auditable<D> {
    // Customize formatting behavior for new and old values
    @Override
    String convertLoggedPropertyToString(String propertyName, Object value) {
        if (value instanceof MySpecialThing) {
            return ((MySpecialThing)value).formatAsString()
        }
        super.convertLoggedPropertyToString(propertyName, value)
    }
    
    // Customize to populate custom attributes on the audit log entity
    @Override
    boolean beforeSaveLog(Object auditEntity) {
        auditEntity.naturalKey = getNaturalKey()
    }
}
```

=== Request Resolvers
Audit logging usually requires auditing the user and/or request context for a specific action. The plugin supports a pluggable method of resolving request context by registering an `auditRequestResolver` bean that applications can override if needed.

[WARNING]
====
Spring-Security-Core plugin between version 3.2.1 and 4.0.1 has been set to MODE_INHERITABLETHREADLOCAL. This can lead to current actor problems, as it might keep the former security context. This setting was changed back to the secure MODE_THREADLOCAL in Spring-Security-Core 4.0.1. If you use a Spring-Security-Core plugin and see weird actor behavior, either upgrade the Spring-Security-Core plugin, or explicitly set the SecurityContextHolder strategy to MODE_THREADLOCAL.
https://github.com/grails/grails-spring-security-core/issues/587
====

==== Audit Request Resolvers
The plugin ships with two resolvers:

```groovy
class DefaultAuditRequestResolver implements AuditRequestResolver { ... }
```

and

```groovy
class SpringSecurityRequestResolver extends DefaultAuditRequestResolver { ... }
```

The default resolver uses the `principal` in the active GrailsWebRequest to resolve the Actor name and Request URI for logging purposes.

If a bean named `springSecurityService` is available, the second resolver is registered which uses the `currentUser()` method to resolve the user context.

For other authentication strategies, you can implement and override the `auditRequestResolver` bean with your own implementation of:

```groovy
interface AuditRequestResolver {
    /**
     * @return the current actor
     */
    String getCurrentActor()

    /**
     * @return the current request URI or null if no active request
     */
    String getCurrentURI()
}
```

Just register your resolver in `resources.groovy`:

```groovy
beans = {
    auditRequestResolver(CustomAuditRequestResolver) {
        customService = ref('customService')
    }
}
```

Below are a few examples for other common security frameworks. 

===== Acegi Plugin

```groovy
/**
 * @author Jorge Aguilera
 */
class AcegiAuditResolver extends DefaultAuditRequestResolver {
    def authenticateService

    @Override
    String getCurrentActor() {
        authenticateService?.principal()?.username ?: super.getCurrentActor()
    }    
}
```

===== CAS Authentication

```groovy
import edu.yale.its.tp.cas.client.filter.CASFilter

class CASAuditResolver extends DefaultAuditRequestResolver {
    def authenticateService

    @Override
    String getCurrentActor() {
        GrailsWebRequest request = GrailsWebRequest.lookup()
        request?.session?.getAttribute(CASFilter.CAS_FILTER_USER)
    }    
}
```

===== Shiro Plugin
```groovy
@Component('auditRequestResolver')
class ShiroAuditResolver extends DefaultAuditRequestResolver {
    @Override
    String getCurrentActor() {
        org.apache.shiro.SecurityUtils.getSubject()?.getPrincipal()
    }    
}
```
